apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

final MIN_SDK_VERSION           = 15
final COMPILE_SDK_VERSION       = 23
final BUILD_TOOLS_VERSION       = '23.0.3'
final VERSION_CODE              = 3
final VERSION_NAME              = '0.1.2'
final SITE_URL                  = 'https://github.com/HPZhang/OrcaSwitchLibrary'
final GIT_URL                   = 'https://github.com/HPZhang/OrcaSwitchLibrary.git'
final DEVELOPER_NAME            = 'hpzhang'
final DEVELOPER_EMAIL           = 'use_to_work@163.com'

group   = 'com.orcanote.orcaswitch'
version = VERSION_NAME

Properties props = new Properties()
props.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion    MIN_SDK_VERSION
        targetSdkVersion COMPILE_SDK_VERSION
        versionCode      VERSION_CODE
        versionName      VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION   = '23.2.1'

    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"

    testCompile 'junit:junit:4.12'
}

bintray {
    user           = props.getProperty("bintray.user")
    key            = props.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo                  = 'maven'
        name                  = 'orcaswitch'
        desc                  = 'A custom view for switch.'
        websiteUrl            = SITE_URL
        issueTrackerUrl       = "$SITE_URL/issues"
        vcsUrl                = GIT_URL
        licenses              = ['Apache-2.0']
        labels                = ['android', 'switch', 'view']
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging  'aar'
            name       'OrcaSwitch'
            url        SITE_URL
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url  'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id    DEVELOPER_NAME
                    name  DEVELOPER_NAME
                    email DEVELOPER_EMAIL
                }
            }
            scm {
                url                 SITE_URL
                connection          GIT_URL
                developerConnection GIT_URL

            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}